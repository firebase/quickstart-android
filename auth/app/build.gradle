apply plugin: 'com.android.application'
check.dependsOn 'assembleMinSdkJellybeanDebugAndroidTest', 'assembleMinSdkIcsDebugAndroidTest'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    flavorDimensions "minSdkVersion"

    defaultConfig {
        applicationId "com.google.firebase.quickstart.auth"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {

        // Build variant with minSdk 16 to include Facebook and FirebaseUI libraries.
        minSdkJellybean {
            dimension "minSdkVersion"
            minSdkVersion 16
        }

        // Build variant with minSdk 14, excludes Facebook and FirebaseUI libraries.
        minSdkIcs {
            dimension "minSdkVersion"
            minSdkVersion 14
        }

    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:25.3.1'
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:animated-vector-drawable:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'

    // Firebase Authentication
    compile 'com.google.firebase:firebase-auth:11.0.1'

    // Google Sign In SDK (only required for Google Sign In)
    compile 'com.google.android.gms:play-services-auth:11.0.1'

    // Firebase UI
    // Used in FirebaseUIActivity. The FirebaseUI-Android library has a minSdk level of 16
    // so this library is only included in the minSdkJellybean variant.
    minSdkJellybeanCompile 'com.firebaseui:firebase-ui-auth:1.2.0'

    // Facebook Android SDK (only required for Facebook Login)
    // Used in FacebookLoginActivity. This is only compiled into the minSdkJellybean variant of this
    // app. You can build a 'minSdkGingerbread' variant to test on devices with 9 <= SDK <= 15.
    minSdkJellybeanCompile 'com.facebook.android:facebook-android-sdk:4.9.0'
    minSdkJellybeanCompile 'com.android.support:customtabs:25.3.1'

    // Twitter Android SDK (only required for Twitter Login)
    compile('com.twitter.sdk.android:twitter-core:1.6.6@aar') {
        transitive = true
    }
    compile('com.twitter.sdk.android:twitter:1.13.1@aar') {
        transitive = true
    }

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
}

apply plugin: 'com.google.gms.google-services'
