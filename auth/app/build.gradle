apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.google.firebase.quickstart.auth"
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        // Build variant to include the Facebook Android SDk
        // The Facebook Android SDK has a min SDK version of 15
        facebook {
            minSdkVersion 15
        }

        // Build variant to exclude the Facebook Android SDK
        // Firebase Authentication has a min SDK version of 9
        nofacebook {
            minSdkVersion 9
        }

    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:24.0.0'
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.0.0'

    // Firebase Authentication
    compile 'com.google.firebase:firebase-auth:9.4.0'

    // Google Sign In SDK (only required for Google Sign In)
    compile 'com.google.android.gms:play-services-auth:9.4.0'

    // Facebook Android SDK (only required for Facebook Login)
    // This is only compiled into the 'facebook' variant of this app. You can build
    // a 'nofacebook' variant to test on devices with SDK < 15.
    facebookCompile 'com.facebook.android:facebook-android-sdk:4.9.0'

    // Twitter Android SDK (only required for Twitter Login)
    compile('com.twitter.sdk.android:twitter-core:1.6.6@aar') {
        transitive = true
    }
    compile('com.twitter.sdk.android:twitter:1.13.1@aar') {
        transitive = true;
    }

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
}

apply plugin: 'com.google.gms.google-services'
