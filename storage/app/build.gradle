apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.google.firebase.quickstart.firebasestorage"
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:24.0.0'
}

// Based on: http://goo.gl/cuAayN
// To run a test with permission granted:
//  ./gradlew :app:grantDebugPermissions :app:runDebugEspressoTests
android.applicationVariants.all { variant ->
    def applicationId = variant.applicationId
    def adb = android.getAdbExe().toString()
    def variantName = variant.name.capitalize()
    def grantPermissionTask = tasks.create(name: "grant${variantName}Permissions", type: Exec) {
        "${adb} devices".execute().text.eachLine {
            if (it.endsWith("device")) {
                def device = it.split()[0]
                commandLine(adb, "-s", device, "shell", "pm", "grant", applicationId,
                        "android.permission.WRITE_EXTERNAL_STORAGE")
                commandLine(adb, "-s", device, "shell", "pm", "grant", applicationId,
                        "android.permission.READ_EXTERNAL_STORAGE")
            }
        }
    }

    grantPermissionTask.dependsOn "install${variantName}"
    grantPermissionTask.dependsOn "install${variantName}AndroidTest"

    def runEspressoTestsTask = tasks.create(name: "run${variantName}EspressoTests", type: Exec) {
        commandLine(adb, "shell", "am", "instrument", "-w",  "-r", "-e", "debug", "false", "-e",
                "class", "${applicationId}.MainActivityTest",
                "${applicationId}.test/android.support.test.runner.AndroidJUnitRunner")
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.0.0'

    compile 'pub.devrel:easypermissions:0.1.9'

    compile 'com.google.firebase:firebase-auth:9.4.0'
    compile 'com.google.firebase:firebase-storage:9.4.0'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
}

apply plugin: 'com.google.gms.google-services'
