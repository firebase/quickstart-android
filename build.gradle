buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.0'

        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:perf-plugin:1.3.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.2.1'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
}

allprojects {
    repositories {
        google()
        //mavenLocal() must be listed at the top to facilitate testing
        mavenLocal()
        jcenter()
    }

    def isNonStable = { candidate ->
        return ['alpha', 'beta', 'rc', 'SNAPSHOT'].any { word ->
            return candidate.version.contains(word)
        }
    }

    def isBlackListed = { candidate ->
        def blacklist = [
                'androidx.browser:browser',
                'com.facebook.android',
                'com.google.guava',
                'com.github.bumptech.glide'
        ]
        return blacklist.any { word ->
            return candidate.toString().contains(word)
        }
    }

    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate) || isBlackListed(it.candidate)
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
}

task("ktlint", type: JavaExec, group: "verification") {
    def outputDir = "${project.buildDir}/reports/ktlint/"
    def inputFiles = project.fileTree(dir: "src", include: "**/*.kt")
    def outputFile = "${outputDir}ktlint-checkstyle-report.xml"

    // See:
    // https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
    inputs.files(inputFiles)
    outputs.dir(outputDir)

    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args = [
        "--format",
        "--android",
        "--reporter=plain",
        "--reporter=checkstyle,output=${outputFile}",
        "**/*.kt",
    ]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
